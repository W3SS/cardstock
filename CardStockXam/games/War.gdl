;; War in the GDL
(game
    (setup  
     ;; Set up the players, 2 players each on their own team
      (create players 2)
      (create teams (0) (1))
      ;; Create the deck source

      ;;TODO
      ;;(create deck (game iloc STOCK) (deck (2)))
      (create deck (game iloc STOCK) (deck (RANK (A, 2, 3, 4, 5, 6, 7, 8, 9, 10, J, Q, K))
                                                     (color (RED (suit (HEARTS, DIAMONDS)))
                                                           (BLACK (suit (CLUBS, SPADES))))))
    )
    (do (
        ;; Create the deck source
        (put points 'PRECEDENCE (
                               ((RANK (a)) 14)
                               ((RANK (k)) 13) 
                               ((RANK (q)) 12)
                               ((RANK (j)) 11)
                               ((RANK (10)) 10)
                               ((RANK (9)) 9)
                               ((RANK (8)) 8)
                               ((RANK (7)) 7)
                               ((RANK (6)) 6)
                               ((RANK (5)) 5)
                               ((RANK (4)) 4)
                               ((RANK (3)) 3)
                               ((RANK (2)) 2)
                               ))      
        (shuffle (game iloc STOCK))
        (repeat 26
          (all player 'P
            (move (top (game iloc STOCK))
                  (top ('P iloc HAND)))))
   ))
               
   ;; Stages of the game
   (stage player
      (end (any player 'P (== (size ('P iloc HAND)) 0)))
                           
      ;; Each player plays a card
      (stage player
          (end (all player 'P (>= (size ('P vloc TRICK)) 1)))
          ;; players play a card
          (choice (
              ((== (game sto WAR) 0)
               (do (
                 (move (top ((current player) iloc HAND))
                       (top ((current player) vloc TRICK)))
                 (move (top ((next player) iloc HAND))
                           (top ((next player) vloc TRICK)))
              )))
              (all player 'P
                 ((and (== (game sto WAR) 1)
                       (>= (size ('P iloc HAND)) 4))
               (do (
                 (repeat 3 (move (top ((current player) iloc HAND))
                           (top (game iloc BOUNTY))))
                 
                 (move (top ((current player) iloc HAND))
                           (top ((current player) vloc TRICK)))
                 (repeat 3 (move (top ((next player) iloc HAND))
                           (top (game iloc BOUNTY))))
                 (move (top ((next player) iloc HAND))
                           (top ((next player) vloc TRICK)))
              ))))
              ((and (== (game sto WAR) 1) (any player 'P (== (size ('P iloc HAND)) 3)) (all player 'P (!= (size ('P iloc HAND)) 2)) (all player 'P (!= (size ('P iloc HAND)) 1))) ;;TODO
               (do ( 
                 (repeat 2 (move (top ((current player) iloc HAND))
                           (top (game iloc BOUNTY))))
                 (move (top ((current player) iloc HAND))
                           (top ((current player) vloc TRICK)))
                 (repeat 2 (move (top ((next player) iloc HAND))
                           (top (game iloc BOUNTY))))
                 (move (top ((next player) iloc HAND))
                           (top ((next player) vloc TRICK)))
              )))
              ((and (== (game sto WAR) 1)
                    (any player 'P (== (size ('P iloc HAND)) 2))
                    (all player 'P (!= (size ('P iloc HAND)) 1)))
               
               (do (
                 (move (top ((current player) iloc HAND))
                       (top (game iloc BOUNTY)))
                 (move (top ((current player) iloc HAND))
                       (top ((current player) vloc TRICK)))
                 (move (top ((next player) iloc HAND))
                       (top (game iloc BOUNTY))
                       )
                 (move (top ((next player) iloc HAND))
                       (top ((next player) vloc TRICK)))
              )))
              ((and (== (game sto WAR) 1)
                    (any player 'P (== (size ('P iloc HAND)) 1))
               (do (
                 (move (top ((current player) iloc HAND))
                           (top ((current player) vloc TRICK)))
                 (move (top ((next player) iloc HAND))
                           (top ((next player) vloc TRICK)))
              )))
          ))
      )

      (do (
          
          ((!= (cardatt RANK (top ((current player) vloc TRICK)))
               (cardatt RANK (top ((next player) vloc TRICK))))
           (do (
             (all player 'P
                   (move (top (game iloc BOUNTY))
                         (bottom (((owner (max (union ('P vloc TRICK)) using 'PRECEDENCE)) player) iloc HAND)) all))
             (all player 'P
                   (move (top ((all player) vloc TRICK))
                       (bottom (((owner (max (union ('P vloc TRICK)) using 'PRECEDENCE)) player) iloc HAND)) all))
             (set (game sto WAR) 0)
           ))
          )
          
          ((== (cardatt RANK (top ((current player) vloc TRICK)))
               (cardatt RANK (top ((next player) vloc TRICK))))
           (do (
             (all player 'P
                   (move (top ('P vloc TRICK))
                         (top (game iloc BOUNTY)) all))
             (set (game sto WAR) 1)
          ))
         )
      ))
   )
         
   (scoring max (size ((current player) iloc HAND)))
)